apply plugin: 'com.android.application'

android {

    signingConfigs {
        test {
            keyAlias '123456'
            keyPassword '123456'
            storeFile file("test.jks")
            storePassword '123456'
        }
    }

    def globalConfiguration = rootProject.extensions.getByName("ext")
    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]
    defaultConfig {
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]

        applicationId globalConfiguration["androidApplicationId"]
        versionCode globalConfiguration["androidVersionCode"]
        versionName globalConfiguration["androidVersionName"]
        testInstrumentationRunner globalConfiguration["testInstrumentationRunner"]
        testApplicationId globalConfiguration["testApplicationId"]
        setProperty("archivesBaseName", applicationId + "-v" + versionCode + "(" + versionName + ")")
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        QA {
            initWith release
            signingConfig signingConfigs.test
            applicationIdSuffix ".QA"
        }
        debug {
            minifyEnabled false
            shrinkResources false
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}


dependencies {
    implementation project(':domain')
    implementation project(':data')
    def presentationDependencies = rootProject.ext.presentationDependencies
    def presentationTestDependencies = rootProject.ext.presentationTestDependencies
    implementation presentationDependencies.supportLibrary
    implementation presentationDependencies.supportLibraryDesign
    implementation presentationDependencies.cardView
    implementation presentationDependencies.recyclerView
    implementation presentationDependencies.constraintLayout
    implementation presentationDependencies.supportMedia
    implementation presentationDependencies.rxAndroid
    implementation presentationDependencies.rxPermissions
    implementation presentationDependencies.acra
    implementation presentationDependencies.moxy
    annotationProcessor presentationDependencies.moxyCompiler
    implementation presentationDependencies.butterKnife
    annotationProcessor presentationDependencies.butterKnifeCompiler
    implementation presentationDependencies.dagger
    annotationProcessor presentationDependencies.daggerCompiler
    implementation presentationDependencies.room
    annotationProcessor presentationDependencies.roomCompiler
    compileOnly presentationDependencies.javaxAnnotation
    testImplementation presentationTestDependencies.junit
    testImplementation presentationTestDependencies.mockito
    androidTestImplementation presentationTestDependencies.dexmaker
    androidTestImplementation presentationTestDependencies.dexmakerMockito
    androidTestImplementation presentationTestDependencies.mockito
    androidTestImplementation presentationTestDependencies.espresso
}