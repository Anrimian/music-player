plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {

    signingConfigs {
        test {
            keyAlias '123456'
            keyPassword '123456'
            storeFile file("test.jks")
            storePassword '123456'
        }
    }

    namespace 'com.github.anrimian.musicplayer.lite'

    def config = rootProject.ext

    compileSdk config.androidCompileSdkVersion

    defaultConfig {
        minSdkVersion config.androidMinSdkVersion
        targetSdkVersion config.androidTargetSdkVersion

        applicationId 'com.github.anrimian.musicplayer'
        versionCode 176
        versionName "0.9.8.1"
        testInstrumentationRunner config.testInstrumentationRunner
        testApplicationId "${applicationId}.test"
        archivesBaseName = "$applicationId-v$versionName-b$versionCode"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-lite.pro'
        }
        QA {
            initWith release
            signingConfig signingConfigs.test
            applicationIdSuffix ".QA"
        }
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    packagingOptions {
        jniLibs {
            excludes += ['**/kotlin/**']
        }
        resources {
            excludes += [
                    '/META-INF/*.kotlin_module',
                    '**/kotlin/**',
                    '**/*.txt',
                    '**/*.xml',
                    '/*.properties'
            ]
        }
    }
}

dependencies {
    implementation project(':domain')
    implementation project(':data')
    implementation project(':app')

    implementation project(':libs:filesync:core')

    implementation appCompat

    implementation rxAndroid

    implementation dagger
    kapt daggerCompiler

    compileOnly javaxAnnotation
    compileOnly javaxGeneratedAnnotation
}